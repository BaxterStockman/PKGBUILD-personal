# $Id: PKGBUILD 211702 2017-02-10 07:51:44Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Iwan Timmer <irtimmer@gmail.com>
# Contributor: Vincent Demeester <vincent@sbr.io>
# Contributor: Josh VanderLinden <arch@cloudlery.com>

pkgname=('docker-compose' 'docker-compose2')
pkgver=1.11.1
pkgrel=1
pkgdesc="Fast, isolated development environments using Docker"
arch=('i686' 'x86_64')
url="https://www.docker.com/"
license=("Apache")
depends=('python-cached-property' 'python-colorama' 'python-docopt' 'python-yaml' 'python-requests'
         'python-texttable' 'python-websocket-client' 'python-docker' 'python-dockerpty'
         'python-six' 'python-jsonschema' 'python-setuptools')
depends_x86_64+=('docker')
makedepends=('git')
checkdepends=('python-pytest-runner' 'python-mock' 'python2-pytest-runner' 'python2-mock')
source=("git+https://github.com/docker/compose.git#tag=$pkgver")
sha256sums=('SKIP')

prepare() {
  cd compose

  # Remove upper bound on requires
  sed -i 's/==/>=/g' requirements.txt
  sed -i "s/, < .*',$/',/" setup.py

  cd "$srcdir"
  cp -a compose{,2}
}

build() {
  cd "$srcdir/compose"

  ./script/build/write-git-sha
  python setup.py build

  cd "$srcdir/compose2"
  ./script/build/write-git-sha
  python2 setup.py build
}

check() {
  # Hack entry points by installing it
  # TODO: need a running docker daemon to test

  cd "${srcdir}/compose"
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  # PATH="$PWD/tmp_install/usr/bin:$PATH" PYTHONPATH="$PWD/tmp_install/usr/lib/python3.6/site-packages:$PYTHONPATH" python setup.py ptr

  cd "${srcdir}/compose2"
  python2 setup.py install --root="$PWD/tmp_install" --optimize=1
}

package_docker-compose() {
  cd "$srcdir/compose"

  python setup.py install -O1 --root="$pkgdir"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 contrib/completion/bash/docker-compose "$pkgdir/usr/share/bash-completion/completions/docker-compose"
}

package_docker-compose2(){
  cd "$srcdir/compose2"

  local -a compose2_depends=('python2-backports.ssl_match_hostname' 'python2-enum34'
                             'python2-functools32')

  for depend in "${depends[@]}"; do
    compose2_depends+=("${depend/#python/python2}")
  done

  depends=("${compose2_depends[@]}")

  python2 setup.py install -O1 --root="$pkgdir"
  mv "$pkgdir/usr/bin/docker-compose" "$pkgdir/usr/bin/docker-compose2"
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" \
    "$pkgdir/usr/lib/python2.7/site-packages/compose/timeparse.py"
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 contrib/completion/bash/docker-compose "$pkgdir/usr/share/bash-completion/completions/docker-compose2"
}
